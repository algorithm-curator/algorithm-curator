buildscript {
    // ext를 통해 build.gradle에서 사용하는 전역변수를 설정
    ext {
        springBootVersion = '2.6.9'
        dependencyManagementVersion = '1.0.11.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
    }
}

subprojects {
    group 'com.ac'    // 하위 모듈을 같은 group 단위로 관리 (ComponentScan에서 사용)
    version '1.0'

    // 적용하려는 플러그인은 아래와 같이 적용
    apply plugin: 'java'
    apply plugin: 'java-library'    // for gradle api, implementation build method
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 11

    repositories {
        mavenCentral()
    }

    // 프로젝트 개발에 공통으로 필요한 의존성 선언
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        compileOnly 'org.projectlombok:lombok'
        runtimeOnly 'com.h2database:h2'
        runtimeOnly 'mysql:mysql-connector-java'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }
}

project(':module-api') {
    dependencies {
        api project(':module-common')
    }
}